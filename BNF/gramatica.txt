<programa> ::= <comentario> <programa> 
             | <instrucao> ";" <programa> 
             | <instrucao> ";"

<comentario> ::= "#" <texto_comentario>

<texto_comentario> ::= <letra_digito> 
                     | <letra_digito> <texto_comentario>

<instrucao> ::= <atribuicao> 
              | <declaracao> 
              | <condicional> 
              | <laco> 
              | <impressao>

<atribuicao> ::= <identificador> "=" <expressao>

<declaracao> ::= <tipo> <identificador_lista>

<tipo> ::= "int" | "float"

<identificador_lista> ::= <identificador> 
                        | <identificador> "," <identificador_lista>

<condicional> ::= "if" "(" <condicao> ")" "{" <programa> "}" 
                | "if" "(" <condicao> ")" "{" <programa> "}" "else" "{" <programa> "}"

<laco> ::= "while" "(" <condicao> ")" "{" <programa> "}"

<impressao> ::= "print" "(" <identificador> ")" 
              | "print" <identificador>

<expressao> ::= <termo> 
              | <termo> <operador> <expressao>

<condicao> ::= <expressao> <operador_relacional> <expressao>

<termo> ::= <identificador> | <numero>

<identificador> ::= <letra> <identificador_aux>

<identificador_aux> ::= <letra_digito> 
                      | <letra_digito> <identificador_aux>

<numero> ::= <digito> 
           | <digito> "." <digito>

<letra_digito> ::= <letra> | <digito>

<letra> ::= [a-z] | [A-Z]

<digito> ::= [0-9]

<operador> ::= "+" | "-" | "*" | "/"

<operador_relacional> ::= ">" | "<" | "==" | "!=" | ">=" | "<="
